<html>
	<head>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    <title>Particle Transition</title>
    <script src="https://wgld.org/j/minMatrixb.js" type="text/javascript"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/stats.js/r16/Stats.min.js" type="text/javascript"></script>
    <link href="https://fonts.googleapis.com/css2?family=Sen:wght@700&display=swap" rel="stylesheet">
    <script src="https://cdnjs.cloudflare.com/ajax/libs/gsap/2.0.1/TweenMax.min.js"></script>
    <script src="main.js" type="text/javascript"></script>

    <script id="vs_transformOut" type="x-shader/x-vertex">#version 300 es
    layout (location = 0) in vec4 position;
    layout (location = 1) in vec4 color0;
    layout (location = 2) in vec4 color1;
    layout (location = 3) in vec4 velocity;

    uniform float time;
    uniform vec2 mouse;

    out vec4 vColor;
    out vec4 vVelocity;

    #define PI 3.14159265

    //	<https://www.shadertoy.com/view/4dS3Wd>
    //	By Morgan McGuire @morgan3d, http://graphicscodex.com
    //
    float hash(float n) { return fract(sin(n) * 1e4); }
    float hash(vec2 p) { return fract(1e4 * sin(17.0 * p.x + p.y * 0.1) * (0.1 + abs(sin(p.y * 13.0 + p.x)))); }

    float noise(float x) {
        float i = floor(x);
        float f = fract(x);
        float u = f * f * (3.0 - 2.0 * f);
        return mix(hash(i), hash(i + 1.0), u);
    }

    float noise(vec2 x) {
        vec2 i = floor(x);
        vec2 f = fract(x);

        // Four corners in 2D of a tile
        float a = hash(i);
        float b = hash(i + vec2(1.0, 0.0));
        float c = hash(i + vec2(0.0, 1.0));
        float d = hash(i + vec2(1.0, 1.0));

        // Simple 2D lerp using smoothstep envelope between the values.
        // return vec3(mix(mix(a, b, smoothstep(0.0, 1.0, f.x)),
        //			mix(c, d, smoothstep(0.0, 1.0, f.x)),
        //			smoothstep(0.0, 1.0, f.y)));

        // Same code, with the clamps in smoothstep and common subexpressions
        // optimized away.
        vec2 u = f * f * (3.0 - 2.0 * f);
        return mix(a, b, u.x) + (c - a) * u.y * (1.0 - u.x) + (d - b) * u.x * u.y;
    }

    // This one has non-ideal tiling properties that I'm still tuning
    float noise(vec3 x) {
        const vec3 step = vec3(110, 241, 171);

        vec3 i = floor(x);
        vec3 f = fract(x);

        // For performance, compute the base input to a 1D hash from the integer part of the argument and the 
        // incremental change to the 1D based on the 3D -> 1D wrapping
        float n = dot(i, step);

        vec3 u = f * f * (3.0 - 2.0 * f);
        return mix(mix(mix( hash(n + dot(step, vec3(0, 0, 0))), hash(n + dot(step, vec3(1, 0, 0))), u.x),
                    mix( hash(n + dot(step, vec3(0, 1, 0))), hash(n + dot(step, vec3(1, 1, 0))), u.x), u.y),
                mix(mix( hash(n + dot(step, vec3(0, 0, 1))), hash(n + dot(step, vec3(1, 0, 1))), u.x),
                    mix( hash(n + dot(step, vec3(0, 1, 1))), hash(n + dot(step, vec3(1, 1, 1))), u.x), u.y), u.z);
    }

    //
    // Description : Array and textureless GLSL 2D/3D/4D simplex 
    //               noise functions.
    //      Author : Ian McEwan, Ashima Arts.
    //  Maintainer : stegu
    //     Lastmod : 20110822 (ijm)
    //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
    //               Distributed under the MIT License. See LICENSE file.
    //               https://github.com/ashima/webgl-noise
    //               https://github.com/stegu/webgl-noise
    //

    vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

    float mod289(float x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

    vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
    }

    float permute(float x) {
        return mod289(((x*34.0)+1.0)*x);
    }

    vec4 taylorInvSqrt(vec4 r)
    {
    return 1.79284291400159 - 0.85373472095314 * r;
    }

    float taylorInvSqrt(float r)
    {
    return 1.79284291400159 - 0.85373472095314 * r;
    }

    vec4 grad4(float j, vec4 ip)
    {
    const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
    vec4 p,s;

    p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
    p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
    s = vec4(lessThan(p, vec4(0.0)));
    p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www;

    return p;
    }

    // (sqrt(5) - 1)/4 = F4, used once below
    #define F4 0.309016994374947451

    float snoise(vec4 v)
    {
    const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
                            0.276393202250021,  // 2 * G4
                            0.414589803375032,  // 3 * G4
                        -0.447213595499958); // -1 + 4 * G4

    // First corner
    vec4 i  = floor(v + dot(v, vec4(F4)) );
    vec4 x0 = v -   i + dot(i, C.xxxx);

    // Other corners

    // Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
    vec4 i0;
    vec3 isX = step( x0.yzw, x0.xxx );
    vec3 isYZ = step( x0.zww, x0.yyz );
    //  i0.x = dot( isX, vec3( 1.0 ) );
    i0.x = isX.x + isX.y + isX.z;
    i0.yzw = 1.0 - isX;
    //  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
    i0.y += isYZ.x + isYZ.y;
    i0.zw += 1.0 - isYZ.xy;
    i0.z += isYZ.z;
    i0.w += 1.0 - isYZ.z;

    // i0 now contains the unique values 0,1,2,3 in each channel
    vec4 i3 = clamp( i0, 0.0, 1.0 );
    vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
    vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

    //  x0 = x0 - 0.0 + 0.0 * C.xxxx
    //  x1 = x0 - i1  + 1.0 * C.xxxx
    //  x2 = x0 - i2  + 2.0 * C.xxxx
    //  x3 = x0 - i3  + 3.0 * C.xxxx
    //  x4 = x0 - 1.0 + 4.0 * C.xxxx
    vec4 x1 = x0 - i1 + C.xxxx;
    vec4 x2 = x0 - i2 + C.yyyy;
    vec4 x3 = x0 - i3 + C.zzzz;
    vec4 x4 = x0 + C.wwww;

    // Permutations
    i = mod289(i);
    float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
    vec4 j1 = permute( permute( permute( permute (
                i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
            + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
            + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
            + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

    // Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
    // 7*7*6 = 294, which is close to the ring size 17*17 = 289.
    vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

    vec4 p0 = grad4(j0,   ip);
    vec4 p1 = grad4(j1.x, ip);
    vec4 p2 = grad4(j1.y, ip);
    vec4 p3 = grad4(j1.z, ip);
    vec4 p4 = grad4(j1.w, ip);

    // Normalise gradients
    vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;
    p4 *= taylorInvSqrt(dot(p4,p4));

    // Mix contributions from the five corners
    vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
    vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
    m0 = m0 * m0;
    m1 = m1 * m1;
    return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
                + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

    }

    void main(){
        vec4 pos = position;
        vec4 vel = velocity;
        vec3 nPos = pos.xyz;
        vec3 nVel = vel.xyz;
        float t = time;
        nVel += 20.0 * vec3(snoise(vec4(0.2 * nPos, 7.255 + 0.2 * t)), snoise(vec4(0.2 * nPos, 3.553 + 0.2 * t)), snoise(vec4(0.2 * nPos, 1.259 + 0.2 * t)));
        nVel += -nPos * length(nPos) * 0.1;
        nVel *= 0.9 + 0.1 * abs(sin(t));
        nPos += nVel * 0.1;
        float trans = (-cos(t * 0.1) + 1.0) * 0.5;
        nPos.z += fract(t) * 0.01;
        pos.z += noise(nPos * 3.0 * sin(t * 0.1));
        vel = vec4(nVel, vel.w);
        gl_Position = pos;
        vColor = color0 * (1.0 - trans) + color1 * trans;
        vVelocity = vel;
    }
    </script>
    <script id="fs_transformOut" type="x-shader/x-fragment">#version 300 es
    precision highp float;

    out vec4 outColor;

    void main(){
        outColor = vec4(1.0);
    }
    </script>
    <script id="vs_feedbackIn" type="x-shader/x-vertex">#version 300 es
    layout (location = 0) in vec4 position;
    layout (location = 1) in vec4 color0;
    layout (location = 2) in vec4 color1;
    layout (location = 3) in vec4 velocity;

    uniform mat4 vpMatrix;
    uniform float time;

    out vec4 vColor;

    void main(){
        vColor = color0;
        gl_Position = vpMatrix * position;
        gl_PointSize = .01;
    }
    </script>
    <script id="fs_feedbackIn" type="x-shader/x-fragment">#version 300 es
    precision highp float;

    in vec4 vColor;

    out vec4 outColor;

    void main(){
        outColor = vColor;
    }
    </script>
    </head>
    <body>
        <div id="container"></div>
        <div class="img_container">
            <label for="file_upload1">
                Image1
                <input type="file" id="file_upload1">
            </label></br>
            <label for="file_upload2">
                Image2
                <input type="file" id="file_upload2">
            </label>
        </div>
        <div id="canvas_container">
            <canvas id="canvas"></canvas>
        </div>
        <h1>Particle Transition</h1>
    </body>
</html>

<style>
body {
    background-color: rgb(255, 255, 255);
}
#container {
    padding-top: 50px;
    position: fixed;
}
.img_container {
    display: flex;
    margin-top: 10px;
    margin-right: 10px;
    justify-content: flex-end;
}
label > input {
    display:none;
}
label {
    font-family: 'Sen';
    font-size: calc(0.5vw + 15px);
    color: rgb(255, 255, 255);
    background-color: rgba(0, 0, 0, 0);
    cursor: pointer;
    padding: 10px;
    opacity: 0.5;
    transition: all 500ms;
}
label:hover {
    opacity: 1.0;
}
canvas {
    position: absolute;
    top: 0;
    bottom: 0;
    right: 0;
    left: 0;
    z-index: -100;
}
h1 {
    font-family: 'Sen';
    font-size: calc(1vw + 25px);
    left: 50px;
    bottom: 10px;
    position: fixed;
    color: white;
    transition: all 500ms;
}
::-webkit-scrollbar {
  display: none;
    }
</style>
